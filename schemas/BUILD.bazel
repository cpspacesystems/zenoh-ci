load("@rules_python//python:defs.bzl", "py_library")
load("@pip//:requirements.bzl", "requirement")
load("@rules_rust//rust:defs.bzl", "rust_library")
load("@crates//:defs.bzl", "all_crate_deps", "aliases")

genrule(
    name = "sensors_py_gen",
    srcs = ["sensors.fbs"],
    outs = [
        "sensors/Altitude.py",
        "sensors/Gyro.py",
        "sensors/IMU.py",
        "sensors/Vec3.py",
        "sensors/__init__.py",
    ],
    tools = ["@flatbuffers//:flatc"],
    cmd = """
      rm -rf $(@D)/sensors && mkdir -p $(@D)/sensors
      $(location @flatbuffers//:flatc) --python -o $(@D) $(SRCS)
      : > $(@D)/sensors/__init__.py
    """,
)

py_library(
    name = "sensors_py",
    srcs = [":sensors_py_gen"],
    deps = [
        requirement("flatbuffers"),
    ],
    visibility = ["//visibility:public"],
)

genrule(
    name = "sensors_rs_gen",
    srcs = ["sensors.fbs"],
    tools = ["@flatbuffers//:flatc"],
    outs = ["sensors_generated.rs"],
    cmd = """
      rm -f $(@D)/sensors_generated.rs
      $(location @flatbuffers//:flatc) --rust -o $(@D) $(SRCS)
    """,
)

rust_library(
    name = "sensors_rs",
    srcs = [":sensors_rs_gen"],
    crate_root = "sensors_generated.rs",
    edition = "2021",
    aliases = aliases(),
    deps = [
        "@crates//:flatbuffers",
    ],
    visibility = ["//visibility:public"],
)
